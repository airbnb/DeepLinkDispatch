apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jmailen.kotlinter'

sourceCompatibility = 11

apply plugin: 'checkstyle'
apply from: '../publishing.gradle'

dependencies {
  implementation project(':deeplinkdispatch-base')
  implementation deps.jsr305
  implementation deps.javaPoet
  implementation deps.androidXAnnotations
  implementation deps.ksp
  implementation deps.xProcessor

  testImplementation deps.junit
  testImplementation deps.assertJ
  testImplementation deps.android
  // For test compile we need a reference of the DeepLinkDelegate (which has android dependencies)
  // Cannot depend on them from Maven as they are .aar and not .jar files (this is a java project)
  testImplementation fileTree(dir: 'libs', include: ['androidx.localbroadcastmanager-1.0.0-beta1.jar','androidx.core-1.0.0-beta1.jar'])
  testImplementation deps.compileTesting
  testImplementation deps.compileTestingKsp
  testImplementation deps.mockk
  testImplementation deps.kotlinReflect
}

checkstyle {
  configFile rootProject.file('checkstyle.xml')
  showViolations true
  configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "11"
    freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    freeCompilerArgs += "-Xopt-in=androidx.room.compiler.processing.ExperimentalProcessingApi"
  }
}

test {
  jvmArgs '--add-opens=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED'
  jvmArgs '--add-opens=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED'
  jvmArgs '--add-opens=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED'
  jvmArgs '--add-opens=jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED'
  jvmArgs '--add-opens=jdk.compiler/com.sun.tools.javac.jvm=ALL-UNNAMED'
  jvmArgs '--add-opens=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED'
  jvmArgs '--add-opens=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED'
  jvmArgs '--add-opens=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED'
  jvmArgs '--add-opens=jdk.compiler/com.sun.tools.javac.comp=ALL-UNNAMED'
  jvmArgs '--add-opens=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED'
}