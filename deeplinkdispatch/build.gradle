apply plugin: 'com.android.library'

sourceCompatibility = 1.7

apply plugin: 'com.bmuschko.nexus'
apply plugin: 'checkstyle'
apply plugin: 'com.github.dcendents.android-maven'

apply from: '../gradle-maven-push.gradle'

dependencies {
  api project(':deeplinkdispatch-base')
  implementation deps.okio
  implementation deps.jsr305
  implementation deps.localBroadcastManager
  implementation deps.appCompat
  testImplementation deps.junit
  testImplementation deps.assertJ
}

checkstyle {
  configFile rootProject.file('checkstyle.xml')
  showViolations true
  configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
}

android {
  compileSdkVersion androidConfig.compileSdkVersion
  buildToolsVersion androidConfig.buildToolsVersion

  defaultConfig {
    minSdkVersion androidConfig.minSdkVersion
    targetSdkVersion androidConfig.compileSdkVersion
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
}

project.group = GROUP
project.version = VERSION_NAME

modifyPom {
  project {
    name POM_NAME
    description PROJECT_DESCRIPTION
    url PROJECT_URL
    inceptionYear '2015'
    version VERSION_NAME
    groupId GROUP
    artifactId POM_ARTIFACT_ID

    scm {
      url PROJECT_URL
      connection 'scm:https://github.com/airbnb/DeepLinkDispatch.git'
      developerConnection 'scm:git@github.com:airbnb/DeepLinkDispatch.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'airbnb'
        name 'Airbnb'
        email 'android@airbnb.com'
      }
    }
  }
}


extraArchive {
  sources = true
  tests = false
  javadoc = true
}

nexus {
  sign = !isCi()
  repositoryUrl = getReleaseRepositoryUrl()
  snapshotRepositoryUrl = getSnapshotRepositoryUrl()
}
