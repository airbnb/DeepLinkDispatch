// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    apply from: rootProject.file("dependencies.gradle")

    repositories {
        google()
        mavenCentral()
        mavenLocal()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/service/local/repositories/snapshots/content/" }
    }
    dependencies {
        classpath deps.androidPlugin
        classpath deps.kotlinGradlePlugin
        classpath deps.gradleMavenPublishPlugin
        classpath deps.benchmarkGradlePlugin
        // Dokka is needed on classpath for vanniktech publish plugin
        classpath deps.dokkaPlugin
        classpath deps.kspGradlePlugin
        classpath deps.ktlintGradlePlugin
        // Note: This means that the gralde plugin used in the sameple app is not built from source
        // but downloaded form the repository at he version specified in the gradle.properties file.
        // If you are making changes to the gradle plugin, you need to change the version in the
        // gradle.properties file and run ./gradlew publishToMavenLocal in order to see the changes
        // in the sample app.
        classpath "com.airbnb:deeplinkdispatch-gradle-plugin:$VERSION_NAME"
    }
}

allprojects {
    apply from: rootProject.file("dependencies.gradle")

    repositories {
        google()
        mavenCentral()
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        allWarningsAsErrors = true
    }
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

