apply plugin: 'java'

sourceCompatibility = 1.7

apply plugin: 'com.bmuschko.nexus'
apply plugin: 'checkstyle'

sourceSets {
  test {
    java {
      srcDir '../deeplinkdispatch/src/main/java' // In tests we need access to the runtime files from the dld library
    }
  }
}

dependencies {
  implementation project(':deeplinkdispatch-base')
  implementation 'com.google.auto.service:auto-service:1.0-rc3'
  implementation deps.jsr305
  implementation deps.javaPoet
  implementation deps.autoCommon

  testImplementation deps.junit
  testImplementation deps.assertJ
  testImplementation deps.android
  // For test compile we need a reference of the DeepLinkDelegate (which has android dependencies)
  // Cannot depend on them from Maven as they are .aar and not .jar files (this is a java project)
  testImplementation fileTree(dir: 'libs', include: ['androidx.localbroadcastmanager-1.0.0-beta1.jar','androidx.core-1.0.0-beta1.jar'])
  testImplementation deps.compileTesting
  testImplementation deps.truth
  testImplementation deps.mockito
  testImplementation files(org.gradle.internal.jvm.Jvm.current().getToolsJar())
}

checkstyle {
  configFile rootProject.file('checkstyle.xml')
  showViolations true
  configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
}

project.group = GROUP
project.version = VERSION_NAME

modifyPom {
  project {
    name POM_NAME
    description PROJECT_DESCRIPTION
    url PROJECT_URL
    inceptionYear '2015'
    version VERSION_NAME
    groupId GROUP
    artifactId POM_ARTIFACT_ID

    scm {
      url PROJECT_URL
      connection 'scm:https://github.com/airbnb/DeepLinkDispatch.git'
      developerConnection 'scm:git@github.com:airbnb/DeepLinkDispatch.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'airbnb'
        name 'Airbnb'
        email 'android@airbnb.com'
      }
    }
  }
}

extraArchive {
  sources = true
  tests = false
  javadoc = true
}

nexus {
  sign = !isCi()
  repositoryUrl = getReleaseRepositoryUrl()
  snapshotRepositoryUrl = getSnapshotRepositoryUrl()
}