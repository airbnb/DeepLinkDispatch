plugins {
    id("com.android.application")
    id("checkstyle")
    id("kotlin-android")
    id("com.google.devtools.ksp")
    id('org.jmailen.kotlinter')
}

checkstyle {
    configFile = rootProject.file('checkstyle.xml')
    showViolations = true
    configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
}

android {
    namespace = 'com.airbnb.deeplinkdispatch.sample'
    compileSdk androidConfig.compileSdkVersion

    defaultConfig {
        applicationId "com.airbnb.deeplinkdispatch.sample"
        // Sample app uses minSdk 21 because the test dependency (androidx.test:core) requires it
        minSdk = 21
        targetSdk = androidConfig.compileSdkVersion
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }

    }
    packagingOptions {
        resources {
            excludes += ['META-INF/services/javax.annotation.processing.Processor']
        }
    }
    lint {
        disable 'InvalidPackage'
    }
    testOptions {
        unitTests {
            // Required for Robolectric to access assets from the merged debug assets
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation project(':deeplinkdispatch')
    ksp project(':deeplinkdispatch-processor')
    implementation project(':sample-library')
    implementation project(':sample-kapt-library')
    implementation project(':sample-ksp-library')
    implementation project(':sample-benchmarkable-library')
    implementation deps.appCompat
    implementation deps.localBroadcastManager

    testImplementation deps.androidxTestCore
    testImplementation deps.roboelectric
    testImplementation deps.junit
    testImplementation deps.mockk
}

ksp {
    arg("deepLinkDoc.output", "${buildDir}/doc/deeplinks.txt")
    arg("deepLink.incremental", "true")
    arg("deepLink.customAnnotations",
            "com.airbnb.deeplinkdispatch.sample.AppDeepLink|"
                    + "com.airbnb.deeplinkdispatch.sample.WebDeepLink|"
                    + "com.airbnb.deeplinkdispatch.sample.WebPlaceholderDeepLink|"
                    + "com.airbnb.deeplinkdispatch.sample.library.LibraryDeepLink|"
                    + "com.airbnb.deeplinkdispatch.sample.ksplibrary.ManifestGenWebDeepLink"
    )
}

// Copy KSP library assets to main assets for Robolectric tests.
// This is needed because Robolectric doesn't automatically merge assets from library dependencies
// during unit testing (only during instrumentation tests and actual APK builds).
// The assets are copied to the main assets source set so Robolectric can find them.
tasks.register('copyLibraryAssetsForTest', Copy) {
    from project(':sample-ksp-library').layout.buildDirectory.dir('intermediates/assets/debug/mergeDeepLinkAssetsDebug')
    into layout.buildDirectory.dir('generated/libraryAssets')
    dependsOn ':sample-ksp-library:mergeDeepLinkAssetsDebug'
}

android.sourceSets.debug {
    assets.srcDirs += layout.buildDirectory.dir('generated/libraryAssets')
}

afterEvaluate {
    tasks.findByName('testDebugUnitTest')?.dependsOn('copyLibraryAssetsForTest')
    tasks.findByName('mergeDebugAssets')?.dependsOn('copyLibraryAssetsForTest')
    // Lint tasks also need to depend on the copy task since they scan assets
    tasks.matching { task ->
        def nameLower = task.name.toLowerCase()
        nameLower.contains('lint') && nameLower.contains('debug')
    }.configureEach {
        dependsOn('copyLibraryAssetsForTest')
    }
}
